import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version "$springBootVersion" apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'io.projectreactor:reactor-core'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'io.projectreactor:reactor-test'
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
        dependencies {
            dependencySet(group: 'org.mapstruct', version: mapStructVersion) {
                entry 'mapstruct'
                entry 'mapstruct-processor'
            }

            dependencySet(group: 'org.testcontainers', version: testContainersVersion) {
                entry 'testcontainers'
                entry 'junit-jupiter'
                entry 'postgresql'
            }
        }
    }

    test {
        useJUnitPlatform()
    }
}
